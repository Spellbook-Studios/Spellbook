import java.util.regex.Matcher
import java.util.regex.Pattern

plugins {
    id "spellbook-gradle-lib" version '1.0'
    id 'maven-publish'
    id 'signing'
}

spellbook {
    spellbookVersion = ""
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.10.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()

    maxHeapSize = '1G'

    testLogging {
        events "passed", "skipped", "failed"
    }
}

// GET VERSION
Matcher m = Pattern.compile("SPELLBOOK_VERSION = \"(.*?)\"").matcher(rootProject.file('spellbook/src/main/java/dk/sebsa/Spellbook.java').text.trim())
m.find()
version = m.group(1)
group = 'dk.sebsa'

// UTF 8
compileJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
} // END

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}
// Publishing
publishing {
    repositories {
        maven {
            name = "Spellbook-Repository"
            def releasesRepoUrl = "https://maven.sebsa.dk/releases"
            def snapshotsRepoUrl = "https://maven.sebsa.dk/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                artifactId 'spellbook'
                packaging 'jar'
                name = "Spellbook"
                description = "Cross platform JVM application & game development framework"
                url = "https://github.com/Spellbook-Studios/Spellbook"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://raw.githubusercontent.com/Spellbook-Studios/Spellbook/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sebs'
                        name = 'Sebastian August Snoer'
                        email = 'ssnoer@proton.me'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/Spellbook-Studios/Spellbook.git'
                    developerConnection = 'scm:git:ssh://github.com/Spellbook-Studios/Spellbook.git'
                    url = 'https://github.com/Spellbook-Studios/Spellbook'
                }
            }
        }
    }
}